plugins {
    id 'dev.architectury.loom' version "1.4-SNAPSHOT"
    id 'maven-publish'
}

archivesBaseName = project.archives_base_name
group = project.maven_group
version = "$project.minecraft_version+$project.mod_version"

repositories {
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    forge "net.minecraftforge:forge:${project.forge_version}"

    modCompileOnly "maven.modrinth:oculus:${project.iris_version}"

    implementation "org.apiguardian:apiguardian-api:${apiguardian_version}"

    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:0.2.1"))
    implementation(include("io.github.llamalad7:mixinextras-forge:0.2.1"))
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

loom {
    accessWidenerPath = file("src/main/resources/satin.accesswidener")

    forge {
        convertAccessWideners = true

        mixinConfigs = [
                "mixins.satin.client.json"
        ]
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "ConstructLegacy"
            url = "https://repo.constructlegacy.ru/public"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}